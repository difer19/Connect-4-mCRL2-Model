% Data representation model
sort Token = struct O|X|void;
	 Position = List(List(Token));

% Functions
map Rows: Nat;
	Columns: Nat;
	initPosition: Position;
	next: Token->Token;
	isWinner: Position#Token->Bool;
	isFinal: Position -> Bool;

 	replaceBoard: Position#Nat#Nat#Token->Position;
 	replaceColumn: List(Token)#Nat#Token->List(Token);

	getCol: Position#Nat -> List(Token);
	getRowValue: List(Token) -> Nat;

	regList: List(Position)#Nat#Nat#List(Position)#Token -> List(Position);

	maxDepth: Nat;

% variables for the equations of the functions
var m: Token;
	b: Position;
	llst: Position;
 	i, j: Nat;
 	e, n: Token;
	ls: List(Token);
	columnIndex: Nat;
    lp : List(Position);
	nl : List(Position);


% equations describing the operation of the functions
eqn 
	Rows = 6;
	Columns = 7;
	maxDepth = 3;
	initPosition = [
		[void,void,void,void,void,void,void],
		[void,void,void,void,void,void,void],
		[void,void,void,void,void,void,void],
		[void,void,void,void,void,void,void],
		[void,void,void,void,void,void,void],
		[void,void,void,void,void,void,void]
	];

	next(X) = O;
	next(O) = X;
	
	isWinner(b,m) =
 		(exists row: Nat.(row<Rows && 
			((b.row.0==m && b.row.1==m && b.row.2==m && b.row.3==m) ||
			(b.row.1==m && b.row.2==m && b.row.3==m && b.row.4==m) ||
			(b.row.2==m && b.row.3==m && b.row.4==m && b.row.5==m) ||
			(b.row.3==m && b.row.4==m && b.row.5==m && b.row.6==m) ))) ||
											
 		(exists col: Nat.(col<Columns && 
			((b.0.col==m && b.1.col==m && b.2.col==m && b.3.col==m) ||
			(b.1.col==m && b.2.col==m && b.3.col==m && b.4.col==m) ||
			(b.2.col==m && b.3.col==m && b.4.col==m && b.5.col==m) ))) ||
 		
		(b.0.0  == m && b.1.1  == m && b.2.2  == m && b.3.3  == m ) ||
		(b.0.1  == m && b.1.2  == m && b.2.3  == m && b.3.4  == m ) ||
		(b.0.2  == m && b.1.3  == m && b.2.4  == m && b.3.5  == m ) ||
		(b.0.3  == m && b.1.4  == m && b.2.5  == m && b.3.6  == m ) ||
		(b.1.0  == m && b.2.1  == m && b.3.2  == m && b.4.3  == m ) ||
		(b.1.1  == m && b.2.2  == m && b.3.3  == m && b.4.4  == m ) ||
		(b.1.2  == m && b.2.3  == m && b.3.4  == m && b.4.5  == m ) ||
		(b.1.3  == m && b.2.4  == m && b.3.5  == m && b.4.6  == m ) ||
		(b.2.0  == m && b.3.1  == m && b.4.2  == m && b.5.3  == m ) ||
		(b.2.1  == m && b.3.2  == m && b.4.3  == m && b.5.4  == m ) ||
		(b.2.2  == m && b.3.3  == m && b.4.4  == m && b.5.5  == m ) ||
		(b.2.3  == m && b.3.4  == m && b.4.5  == m && b.5.6  == m ) ||
		(b.0.3  == m && b.1.2  == m && b.2.1  == m && b.3.0  == m ) ||
		(b.0.4  == m && b.1.3  == m && b.2.2  == m && b.3.1  == m ) ||
		(b.0.5  == m && b.1.4  == m && b.2.3  == m && b.3.2  == m ) ||
		(b.0.6  == m && b.1.5  == m && b.2.4  == m && b.3.3  == m ) ||
		(b.1.3  == m && b.2.2  == m && b.3.1  == m && b.4.0  == m ) ||
		(b.1.4  == m && b.2.3  == m && b.3.2  == m && b.4.1  == m ) ||
		(b.1.5  == m && b.2.4  == m && b.3.3  == m && b.4.2  == m ) ||
		(b.1.6  == m && b.2.5  == m && b.3.4  == m && b.4.3  == m ) ||
		(b.2.3  == m && b.3.2  == m && b.4.1  == m && b.5.0  == m ) ||
		(b.2.4  == m && b.3.3  == m && b.4.2  == m && b.5.1  == m ) ||
		(b.2.5  == m && b.3.4  == m && b.4.3  == m && b.5.2  == m ) ||
		(b.2.6  == m && b.3.5  == m && b.4.4  == m && b.5.3  == m ) ;

	isFinal(b) = isWinner(b, X) || isWinner(b, O) || 
			!exists row, column : Nat.(row < Rows && column < Columns && b.row.column == void);
	


 	replaceBoard ([], i, j, n)=[];

 	replaceBoard(ls|>llst,i,j,n)=if(i==0,replaceColumn(ls,j,n)|>llst,
 			ls|>replaceBoard(llst, Int2Nat(i-1), j, n));

	replaceColumn([], j, n) = [];

 	replaceColumn(e|>ls,j,n) = if(j==0,n|>ls,
		 e|>replaceColumn(ls, Int2Nat(j-1), n));

	getCol(b, columnIndex) =
    	if(columnIndex < Columns, 
		[b.0.columnIndex, b.1.columnIndex, b.2.columnIndex, b.3.columnIndex, b.4.columnIndex, b.5.columnIndex], 
	[]);

	getRowValue([]) = 8;
	getRowValue(ls) = if(rhead(ls)==void, #ls, getRowValue(rtail(ls)));

	regList(lp, i, j, nl, m) =
		if(i == #lp, nl, if(j < Columns, regList(lp, i,Int2Nat(j+1), 
			nl<|replaceBoard(lp.i, Int2Nat((getRowValue(getCol(lp.i,j)))-1),j,m), m),
			regList(lp, Int2Nat(i+1), 0, nl, m)));

% actions
act backtracking, defSuccessor, defSuccessorGen, backtrackingParallel;
	max : Nat#Nat#Nat#Token;
	min : Nat#Nat#Nat#Token;
	place: Nat#Nat#Token;

% Generation of successors
proc defSuc(b: List(Position), i: Nat, j: Nat, nl: List(Position), m: Token, depth: Nat, thread: Nat) =
	Minmax(regList(b, i, j, nl, m), m, depth, thread);

% Main process
proc Minmax(b: List(Position), m: Token, depth: Nat, thread: Nat) = 
	(depth <= maxDepth ) -> 
		(m == X) -> max(#b, depth, thread, m).defSuc(b, 0, 0, [], next(m), Int2Nat(depth+1), thread)
        <> min(#b, depth, thread, m).defSuc(b, 0, 0, [], next(m), Int2Nat(depth+1), thread)
	<> backtracking.defSuccessor.delta;

% Process pool generation
proc processPool(b: Position, m: Token, depth: Nat, column: Nat, thread: Nat) =
	(void in getCol(b,column) && depth < maxDepth) -> 
		Minmax([replaceBoard(b, Int2Nat((getRowValue(getCol(b,column)))-1),column,m)], m, Int2Nat(depth+1), thread);

% Initialization
init allow ({defSuccessorGen, min|min|min|min|min|min|min, 
			max|max|max|max|max|max|max, max, min, backtrackingParallel}, 
		comm({defSuccessor|defSuccessor|defSuccessor|defSuccessor|defSuccessor|defSuccessor|defSuccessor -> defSuccessorGen,
			backtracking|backtracking|backtracking|backtracking|backtracking|backtracking|backtracking -> backtrackingParallel},
			processPool(initPosition, X, 0, 0, 1) || processPool(initPosition, X, 0, 1, 2) ||
			processPool(initPosition, X, 0, 2, 3)|| processPool(initPosition, X, 0, 3, 4) ||
			processPool(initPosition, X, 0, 4, 5)|| processPool(initPosition, X, 0, 5, 6) ||
			processPool(initPosition, X, 0, 6, 7)
		));


%  init allow ({defSuccessorGen, min|min|min|min, 
% 			max|max|max|max, max, min, backtrackingParallel}, 
% 		comm({defSuccessor|defSuccessor|defSuccessor|defSuccessor -> defSuccessorGen,
% 			backtracking|backtracking|backtracking|backtracking -> backtrackingParallel},
% 			processPool(initPosition, X, 0, 0, 1) || processPool(initPosition, X, 0, 1, 2) ||
% 			processPool(initPosition, X, 0, 2, 3) || processPool(initPosition, X, 0, 3, 4)
% 		));